buildscript {
    ext.kotlin_version = '1.4.31'
    ext.CI_PROJECT_ID = System.env.CI_PROJECT_ID

    repositories { mavenCentral() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-library'
apply plugin: 'java-test-fixtures'
apply plugin: 'maven-publish'
//apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'


group = 'io.provs'
version = '0.8.13'

repositories {
    mavenCentral()
}

test {
    // set properties for the tests
    def propertiesForTests = ["testdockerwithoutsudo"]
    for (def prop : propertiesForTests) {
        def value = System.getProperty(prop)
        if (value != null) {
            systemProperty prop, value
        }
    }

    useJUnitPlatform {
        if (System.getenv("CI_JOB_TOKEN") != null) {
            excludeTags('containernonci')
        }
    }
}

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
compileTestFixturesJava.options.debugOptions.debugLevel = "source,lines,vars"
compileTestJava.options.debugOptions.debugLevel = "source,lines,vars"

sourceCompatibility = 1.11
targetCompatibility = 1.11

// https://stackoverflow.com/questions/21904269/configure-gradle-to-publish-sources-and-javadoc
java {
    withSourcesJar()
    withJavadocJar()
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.1.0"

    implementation group: 'com.hierynomus', name: 'sshj', version: '0.31.0'

    api "org.slf4j:slf4j-api:1.7.30"
    api "ch.qos.logback:logback-classic:1.2.3"
    api "ch.qos.logback:logback-core:1.2.3"

    testFixturesApi group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}


//create a single Jar with all dependencies excl. Kotlin libs
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': project.version,
                'Main-Class': 'io.provs.core.entry.EntryKt'
    }
    archivesBaseName = project.name + '-all'
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

//create a single Jar with all dependencies excl. Kotlin libs without version-number but with suffix "latest"
task fatJarLatest(type: Jar) {
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': project.version,
                'Main-Class': 'io.provs.core.entry.EntryKt'
    }
    with jar
    archiveFileName = 'provs-fat-latest.jar'
}

//create a single Jar with all dependencies incl. Kotlin libs
task uberJar(type: Jar) {

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    } {
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': project.version,
                'Main-Class': 'io.provs.core.entry.EntryKt'
    }
    archiveClassifier = 'uber'
}

//create a single Jar with all dependencies incl. Kotlin libs - as ...-latest
task uberJarLatest(type: Jar) {

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    } {
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': project.version,
                'Main-Class': 'io.provs.core.entry.EntryKt'
    }
    archiveFileName = 'provs-latest.jar'
}


task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}


publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        if (System.getenv("CI_JOB_TOKEN") != null) {
            // see https://docs.gitlab.com/ee/user/packages/maven_repository/index.html
            maven {
                url "https://gitlab.com/api/v4/projects/$CI_PROJECT_ID/packages/maven"
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Job-Token'
                    value = System.getenv("CI_JOB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        } else {
            mavenLocal()
        }
    }
}